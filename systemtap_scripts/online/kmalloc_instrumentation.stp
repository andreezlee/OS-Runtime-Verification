%{
	#include <linux/kernel.h>
	#include <linux/init.h>
	#include <linux/module.h>
	#include <linux/types.h>
	#include <linux/kallsyms.h>

	typedef struct
	{
		char event[10];
		uint64_t param;
	} monitor_arg_t;

	extern void monitor_spec1(monitor_arg_t);
%}

global spec1
global spec2
global spec3

probe begin {
	spec1 = ("1" in @3)
	spec2 = ("2" in @3)
	spec3 = ("3" in @3)
}

# Instrument kmalloc for only the test process
probe kernel.function("__kmalloc").return { 
	if (execname()==@2){
		monitor_event(0, $return)
	}
}

# Instrument kfree for only the test process
probe kernel.function("kfree") {
	if (execname()==@2){
		monitor_event(1, $x)
	}
}

# Instrument for the ending of the test process
# Please replace the process parameter with the path for your system
probe process(@1).end{
	monitor_event(2, 0)
	exit()
}

# Writes strings in the instrumentation to a file
function monitor_event:long (e:long, a:long) %{
	/* pragma:read:spec1 */
	/* pragma:read:spec2 */
	/* pragma:read:spec3 */
	monitor_arg_t new_event;

	switch(STAP_ARG_e){
		case 0:
			new_event.event = "kmalloc";
			break;
		case 1:
			new_event.event = "kfree";
			break;
		case 2:
			new_event.event = "end";
			break;
		default: break;
	}

	new_event.param = (uint64_t) STAP_ARG_a;
	if (STAP_GLOBAL_GET_spec1){
		monitor_spec1(new_event);
	}
	STAP_RETURN(0);
%}
